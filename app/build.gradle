plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id "com.google.protobuf" version "0.8.17"
}

android {
    namespace 'com.amsavarthan.game.trivia'
    compileSdk 33

    defaultConfig {
        applicationId "com.amsavarthan.game.trivia"
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName "1.0"

        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        debug {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    //Core
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.activity:activity-compose:1.6.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.compose.material3:material3:1.0.0-beta03'
    implementation 'androidx.compose.material:material-icons-extended:1.2.1'

    implementation 'com.google.android.material:material:1.8.0-alpha01'

    //Jetpack Navigation
    implementation "androidx.navigation:navigation-compose:2.5.2"

    //Hilt
    implementation "com.google.dagger:hilt-android:2.44"
    kapt "com.google.dagger:hilt-compiler:2.44"

    //Firebase
    implementation platform("com.google.firebase:firebase-bom:29.0.0")
    implementation "com.google.firebase:firebase-auth-ktx"

    implementation "com.google.android.gms:play-services-auth:20.3.0"

    //Retrofit
    def retrofit="2.9.0"

    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit"

    //OkHttp
    implementation platform("com.squareup.okhttp3:okhttp-bom:4.10.0")
    implementation "com.squareup.okhttp3:okhttp"

    //Moshi
    def moshi_version = "1.13.0"

    implementation "com.squareup.moshi:moshi:$moshi_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"

    //WorkManager
    def work_version = "2.7.1"

    implementation "androidx.work:work-runtime-ktx:$work_version"

    //Hilt Additional
    def hilt_work_version = "1.0.0"

    implementation "androidx.hilt:hilt-work:$hilt_work_version"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'
    kapt "androidx.hilt:hilt-compiler:$hilt_work_version"


    //Datastore
    implementation "androidx.datastore:datastore:1.0.0"
    implementation "com.google.protobuf:protobuf-javalite:3.18.0"

    //Miscellaneous
    implementation "nl.dionsegijn:konfetti-compose:2.0.1"

    //Debug
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.9.1'

}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.19.4"
    }

    // Generates the java Protobuf-lite code for the Protobufs in this project. See
    // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
    // for more information.
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}

kapt {
    correctErrorTypes true
}